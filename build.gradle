plugins {
    id 'java'
    id 'application'
    id "com.github.johnrengelman.shadow" version "4.0.3"
}

group 'moviebase'
version '0.1'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    compile group: 'io.dropwizard', name: 'dropwizard-core', version: '1.3.7'
    compile group: 'commons-io', name: 'commons-io', version: '2.4'
    compile group: 'org.reflections', name: 'reflections', version: '0.9.10'
    compile ('com.smoketurner:dropwizard-swagger:1.3.7-1') {
        //workaround for org.reflections.Reflections ... java.lang.ClassNotFoundException in 0.9.11 version
        exclude (group: 'org.reflections', module:'reflections')
    }

    testCompile group: 'io.dropwizard', name: 'dropwizard-testing', version: '1.3.7'
    testCompile group: 'org.mockito', name: 'mockito-core', version: '2.23.0'
    testCompile group: 'org.assertj', name: 'assertj-core', version: '3.11.1'
    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile group: 'com.openpojo', name: 'openpojo', version: '0.8.10'
    testCompile group: 'pl.pragmatists', name: 'JUnitParams', version: '1.1.1'
    testCompile group: 'nl.jqno.equalsverifier', name:'equalsverifier', version:'3.0.2'
}

compileJava {
    options.compilerArgs << '-parameters'
}

mainClassName = 'lj.moviebase.MovieBaseApplication'

jar {
    manifest {
        attributes 'Main-Class': mainClassName
    }
}

shadowJar {
    mergeServiceFiles()
    exclude 'META-INF/*.DSA'
    exclude 'META-INF/*.RSA'
}

startShadowScripts {
    doLast {
        unixScript.text = unixScript.text.replace(' $APP_ARGS', ' server ../moviebase.yml')
        windowsScript.text = windowsScript.text.replace('CMD_LINE_ARGS=', 'CMD_LINE_ARGS=server ../moviebase.yml')
    }
}

task copyFiles {
    copy {
        from('.') {
            include '*.yml'
        }
        into 'src/dist'
    }
}

shadowDistZip {
    dependsOn(copyFiles)
}

runShadow {
    args = ['server', 'moviebase.yml']
}

run {
    args = ['server', 'moviebase.yml']
}
